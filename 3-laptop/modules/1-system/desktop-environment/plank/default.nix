# Plank Dock
# 
# This dock is used for quickly launching applications.
#
# Launchers are grouped into categories seperated via spaces. The spaces are generated by my plank separator script
# https://github.com/BeatLink/Plank-Separator
# X-GNOME-Autostart-Delay=0
#

{
    services.bamf.enable = true;                                                # Allows plank to know running applications
    home-manager.users.beatlink = { config, lib, pkgs, ... }: 
    let 

        profileDirectory = config.home.profileDirectory;

        plankItems = {
            "01-firefox" =  "file://${profileDirectory}/share/applications/firefox.desktop";
            "02-keepassxc" = "file://${profileDirectory}/share/applications/org.keepassxc.KeePassXC.desktop";
            "03-trilium" = "file://${profileDirectory}/share/applications/Trilium.desktop";
            "04-nemo" = "file://${profileDirectory}/share/applications/nemo.desktop";
            "05-separator1" = "file:///home/beatlink/.local/share/applications/separator1.desktop";
            "06-thunderbird" = "file://${profileDirectory}/share/applications/thunderbird.desktop";
            "07-whatsapp" = "file:///home/beatlink/.local/share/applications/whatsapp.desktop";
            "08-whatsapp-private" = "file:///home/beatlink/.local/share/applications/whatsapp-private.desktop";
            "09-discord" = "file://${profileDirectory}/share/applications/discord.desktop";
            "09-element" = "file://${profileDirectory}/share/applications/element-desktop.desktop";
            "11-separator2" = "file:///home/beatlink/.local/share/applications/separator2.desktop";
            "12-newsflash" = "file://${profileDirectory}/share/applications/io.gitlab.news_flash.NewsFlash.desktop";
            "13-gmusicbrowser" = "file://${profileDirectory}/share/applications/gmusicbrowser.desktop";
            "14-freetube" = "file://${profileDirectory}/share/applications/freetube.desktop";
            "15-stremio" = "file://${profileDirectory}/share/applications/smartcode-stremio.desktop";
            "16-lutris" = "file://${profileDirectory}/share/applications/net.lutris.Lutris.desktop";
            "16-steam" = "file://${profileDirectory}/share/applications/steam.desktop";
            "17-calibre" = "file://${profileDirectory}/share/applications/calibre-gui.desktop";
            "18-io.lmms.LMMS" = "file://${profileDirectory}/share/applications/lmms.desktop";
            "19-timestretcher" = "file:///home/beatlink/.local/share/applications/TimeStretch.desktop";
            "20-pix" = "file://${profileDirectory}/share/applications/pix.desktop";
            "21-separator3" = "file:///home/beatlink/.local/share/applications/separator3.desktop";
            "22-home-assistant" = "file:///home/beatlink/.local/share/applications/home-assistant.desktop";
            "23-nextcloud" = "file:///home/beatlink/.local/share/applications/nextcloud.desktop";
            "24-separator4" = "file:///home/beatlink/.local/share/applications/separator4.desktop";
            "25-tilix" = "file://${profileDirectory}/share/applications/com.gexperts.Tilix.desktop";
            "26-vscodium" = "file://${profileDirectory}/share/applications/codium.desktop";
            "27-virt-manager" = "file:///run/booted-system/sw/share/applications/virt-manager.desktop";                   
            "28-separator5" = "file:///home/beatlink/.local/share/applications/separator5.desktop";
        };

        dockitemFiles = lib.mapAttrs'(name: value: {                  # Generates the dockitem files from the attrset above
            name = ".config/plank/dock1/launchers/${name}.dockitem";
            value.text = ''
                [PlankDockItemPreferences]
                Launcher=${value}
            '';
        }) plankItems;

        dockitemDconfNames = lib.attrsets.mapAttrsToList (name: _: "${name}.dockitem") plankItems;

    in {
        home = {
            packages = [ pkgs.plank ];                                          # Installs Plank from nixpkgs
            persistence = {
                "/Storage/Apps/System/Plank" = {                                # Loads persistent data for plank
                    directories = [
                        ".local/share/plank"
                    ];
                    allowOther = true;
                };
                "/Storage/Apps/System/Separators/" = {                          # Loads custom separators used by panels
                    files = [
                        ".local/share/icons/separator-black-horizontal.png"
                        ".local/share/icons/separator-black-vertical.png"
                        ".local/share/icons/separator-blank.png"
                        ".local/share/icons/separator-white-horizontal.png"
                        ".local/share/icons/separator-white-vertical.png"
                        ".local/share/applications/separator1.desktop"
                        ".local/share/applications/separator2.desktop"
                        ".local/share/applications/separator3.desktop"
                        ".local/share/applications/separator4.desktop"
                        ".local/share/applications/separator5.desktop"
                    ];
                    allowOther = true;
                };
            };
            file = dockitemFiles;
        };
        dconf = {
            enable = true;                                                      # Enables dconf which stores plank settings
            settings = {
                "net/launchpad/plank/docks/dock1" = {
                    alignment = "center";
                    auto-pinning = true;
                    current-workspace-only = false;
                    dock-items = dockitemDconfNames;
                    hide-delay = 200;
                    hide-mode = "dodge-active";
                    icon-size = 48;
                    items-alignment = "center";
                    lock-items = true;
                    monitor = "";
                    offset = 0;
                    pinned-only = false;
                    position = "bottom";
                    pressure-reveal = false;
                    show-dock-item = false;
                    theme = "Gtk+";
                    tooltips-enabled = true;
                    unhide-delay = 100;
                    zoom-enabled = true;
                    zoom-percent = 175;
                };
            };
        };
        systemd.user = {
            services = {
                "plank" = {
                    Unit = {
                        Description = "Plank";
                        After = ["graphical-session-pre.target"];
                        PartOf = ["graphical-session.target"];
                    };
                    Service = {
                        Type = "simple";
                        ExecStart = "${pkgs.plank}/bin/plank"; 
                    };
                    Install.WantedBy = [ "graphical-session.target" ];
                };
                "restart-plank" = {
                    Unit.Description = "Restart Plank";
                    Service = {
                        Type = "oneshot";
                        ExecStart = "${pkgs.bash}/bin/bash -c '${pkgs.systemd}/bin/systemctl --user stop plank.service; ${pkgs.coreutils-full}/bin/sleep 5; ${pkgs.systemd}/bin/systemctl --user start plank.service'";
                    };
                };
            };
            paths.restart-plank = {
                Unit.Description = "Watch Plank launchers folder and restart Plank when it changes";
                Path.PathChanged = "%h/.config/plank/dock1/launchers";
                Install.WantedBy = [ "default.target" ];
            };
        };
    };
}
