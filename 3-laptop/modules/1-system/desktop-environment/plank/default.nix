# Plank Dock
# 
# This dock is used for quickly launching applications.
#
# Settings are stored via Dconf but are exported using the 1-export-dconf-settings.sh script in this folder
#
# Launchers are grouped into categories seperated via spaces. The spaces are generated by my plank separator script
# https://github.com/BeatLink/Plank-Separator
# X-GNOME-Autostart-Delay=0
#

{ lib, pkgs, ... }:
let 

    plankItems = {
        "01-firefox" =  "file:///${pkgs.firefox}/share/applications/firefox.desktop";
        "02-keepassxc" = "file://${pkgs.keepassxc}/share/applications/org.keepassxc.KeePassXC.desktop";
        "03-trilium" = "file://${pkgs.trilium-next-desktop}/share/applications/Trilium.desktop";
        "04-nemo" = "file:///${pkgs.nemo}/share/applications/nemo.desktop";
        "05-separator1" = "file:///home/beatlink/.local/share/applications/separator1.desktop";
        "06-thunderbird" = "file://${pkgs.thunderbird}/share/applications/thunderbird.desktop";
        "07-whatsapp" = "file:///home/beatlink/.local/share/applications/whatsapp.desktop";
        "08-whatsapp-private" = "file:///home/beatlink/.local/share/applications/whatsapp-private.desktop";
        "09-discord" = "file://${pkgs.discord}/share/applications/discord.desktop";
        "09-element" = "file://${pkgs.element-desktop}/share/applications/element-desktop.desktop";
        "11-separator2" = "file:///home/beatlink/.local/share/applications/separator2.desktop";
        "12-newsflash" = "file:///${pkgs.newsflash}/share/applications/io.gitlab.news_flash.NewsFlash.desktop";
        "13-gmusicbrowser" = "file:///home/beatlink/.nix-profile/share/applications/gmusicbrowser.desktop";
        "14-freetube" = "file:///${pkgs.freetube}/share/applications/freetube.desktop";
        "15-stremio" = "file:///${pkgs.stremio}/share/applications/smartcode-stremio.desktop";
        "16-lutris" = "file://${pkgs.lutris}/share/applications/net.lutris.Lutris.desktop";
        "16-steam" = "file://${pkgs.steam}/share/applications/steam.desktop";
        "17-calibre" = "file://${pkgs.calibre}/share/applications/calibre-gui.desktop";
        "18-io.lmms.LMMS" = "file://${pkgs.lmms}/share/applications/lmms.desktop";
        "19-timestretcher" = "file:///home/beatlink/.local/share/applications/TimeStretch.desktop";
        "20-pix" = "file://${pkgs.pix}/share/applications/pix.desktop";
        "21-separator3" = "file:///home/beatlink/.local/share/applications/separator3.desktop";
        "22-home-assistant" = "file:///home/beatlink/.local/share/applications/home-assistant.desktop";
        "23-nextcloud" = "file:///home/beatlink/.local/share/applications/nextcloud.desktop";
        "24-separator4" = "file:///home/beatlink/.local/share/applications/separator4.desktop";
        "25-tilix" = "file:///home/beatlink/.nix-profile/share/applications/com.gexperts.Tilix.desktop";
        "26-vscodium" = "file://${pkgs.vscodium}/share/applications/codium.desktop";
        "27-virt-manager" = "file:///run/booted-system/sw/share/applications/virt-manager.desktop";                   
        "28-separator5" = "file:///home/beatlink/.local/share/applications/separator5.desktop";
    };

    dockitemFiles = lib.attrsets.concatMapAttrs(name: value: {                  # Generates the dockitem files from the attrset above
        ".config/plank/dock1/launchers/${name}.dockitem".text = ''
            [PlankDockItemPreferences]
            Launcher=${value}
        '';
    }) plankItems;

in {
    services.bamf.enable = true;                                                # Allows plank to know running applications
    home-manager.users.beatlink = { pkgs, ... }: {
        home = {
            packages = [ pkgs.plank ];                                          # Installs Plank from nixpkgs
            persistence = {
                "/Storage/Apps/System/Plank" = {                                # Loads persistent data for plank
                    directories = [
                        ".local/share/plank"
                    ];
                    allowOther = true;
                };
                "/Storage/Apps/System/Separators/" = {                          # Loads custom separators used by panels
                    files = [
                        ".local/share/icons/separator-black-horizontal.png"
                        ".local/share/icons/separator-black-vertical.png"
                        ".local/share/icons/separator-blank.png"
                        ".local/share/icons/separator-white-horizontal.png"
                        ".local/share/icons/separator-white-vertical.png"
                        ".local/share/applications/separator1.desktop"
                        ".local/share/applications/separator2.desktop"
                        ".local/share/applications/separator3.desktop"
                        ".local/share/applications/separator4.desktop"
                        ".local/share/applications/separator5.desktop"
                    ];
                    allowOther = true;
                };
            };
            file = {
                ".config/autostart/plank.desktop".source = "${pkgs.plank}/share/applications/plank.desktop";       # Configures plank to autostart on login
            } // dockitemFiles;
        };
        dconf = {
            enable = true;                                                      # Enables dconf which stores plank settings
            settings = {
                "net/launchpad/plank/docks/dock1" = {
                    alignment = "center";
                    auto-pinning = true;
                    current-workspace-only = false;
                    dock-items = lib.attrsets.mapAttrsToList (name: value: name + ".dockitem") plankItems;
                    hide-delay = 200;
                    hide-mode = "dodge-active";
                    icon-size = 48;
                    items-alignment = "center";
                    lock-items = true;
                    monitor = "";
                    offset = 0;
                    pinned-only = false;
                    position = "bottom";
                    pressure-reveal = false;
                    show-dock-item = false;
                    theme = "Gtk+";
                    tooltips-enabled = true;
                    unhide-delay = 100;
                    zoom-enabled = true;
                    zoom-percent = 175;
                };
            };
        };
        systemd.user.services."restart-plank" = {
            Unit = {
                Description = "Restart Plank";
                After = "home-manager-beatlink";
            };
            Service = {
                ExecStart = "${pkgs.bash}/bin/bash -c 'pkill plank && ${pkgs.plank}/bin/plank'"; 
                Type = "oneshot";
                RemainOnExit = false;
            };
            Install = {
                WantedBy = [ "default.target" ];
            };
        };
    };
}
